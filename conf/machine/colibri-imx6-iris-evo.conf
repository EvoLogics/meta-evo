#@TYPE: Machine
#@NAME: Toradex Colibri iMX6DL/S
#@SOC: i.MX6DL
#@DESCRIPTION: Machine configuration for Toradex Colibri iMX6 SOM
#@MAINTAINER: Max Krummenacher <max.krummenacher@toradex.com>

MACHINEOVERRIDES =. "mx6:mx6dl:mx6-evobb:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa9.inc

PREFERRED_PROVIDER_virtual/kernel ??= "linux-toradex"
PREFERRED_PROVIDER_virtual/kernel_use-mainline-bsp ??= "linux-fslc"
KERNEL_DEVICETREE += " \
  imx6dl-colibri-eval-v3.dtb \
  imx6dl-colibri-cam-eval-v3.dtb \
  imx6dl-colibri-aster.dtb \
  imx6dl-colibri-eval-v3-sd18.dtb \
  imx6dl-colibri-iris-v2.dtb \
  "

KERNEL_DEVICETREE_use-mainline-bsp += "imx6dl-colibri-eval-v3.dtb"
KERNEL_IMAGETYPE = "uImage"
# The kernel lives in a seperate FAT partition, don't deploy it in /boot/
#RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

PREFERRED_PROVIDER_u-boot ?= "u-boot-toradex"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-toradex"
PREFERRED_PROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"
PREFERRED_RPROVIDER_u-boot-fw-utils ?= "u-boot-toradex-fw-utils"

PREFERRED_VERSION_u-boot-toradex ?= "2016.11%"

SPL_BINARY = "SPL"
UBOOT_CONFIG ??= "spl"
UBOOT_CONFIG[spl] = "colibri_imx6_defconfig,,u-boot.img"
UBOOT_MAKE_TARGET = ""
UBOOT_ENTRYPOINT_use-mainline-bsp = "0x10008000"

IMAGE_FSTYPES += "ext4 wic"
# wic support
WKS_FILE = "${MACHINE}.wks"

MACHINE_FEATURES += "usbgadget usbhost vfat ext2"
